#!/usr/bin/env -S just --justfile

initial_snapshot_name := 'clean'


# List recipes by default
default:
  just --list

# Install pre-requesites
pre-reqs:
  sudo snap install distrobuilder --classic
  # TODO: Install Incus

# TODO: Create Proxmox image

# TODO: install Incus agent in Proxmox image

# Ensure Incus instance is running
check-incus-instance HOST:
  #!/usr/bin/env sh
  if ! incus list --format csv | grep -i -q "^{{HOST}},running"; then
    echo "⚠️  Instance '{{HOST}}' is not running. Starting it now..."
    incus start {{HOST}}
    sleep 5  # Give some time for it to start
  fi
  echo "✅  Instance '{{HOST}}' is running."

# Attach to instance
attach HOST:
  incus console {{HOST}} --type=vga

# Create a snapshot
snapshot HOST NAME:
  incus snapshot create {{HOST}} {{NAME}}

# Create initial snapshot
snapshot-initial HOST:
  just snapshot {{HOST}} {{initial_snapshot_name}}

# Restore instance to snaphot
restore HOST NAME=initial_snapshot_name:
  just check-incus-instance {{HOST}}
  incus snapshot restore {{HOST}} {{NAME}}
